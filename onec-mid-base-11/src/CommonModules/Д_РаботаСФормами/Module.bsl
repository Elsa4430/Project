
#Область ПрограммныйИнтерфейс 

Процедура ДополнитьФорму(Форма) Экспорт
	
	// НачалоДоработки 
	// Дополняет форму в зависимости от вида документа
	ИмяФормы = Форма.ИмяФормы;
	
	Если ИмяФормы = "Документ.ЗаказПокупателя.Форма.ФормаДокумента" Тогда
		ДобавитьКонтакноеЛицоВГруппаШапкаПраво(Форма);
		ДобавитьСогласованнаяСкидкаИПересчитатьВГруппаШапкаЛево(Форма);		
	ИначеЕсли ИмяФормы = "Документ.ПоступлениеТоваровУслуг.Форма.ФормаДокумента" ИЛИ ИмяФормы = "Документ.РеализацияТоваровУслуг.Форма.ФормаДокумента" Тогда
		ДобавитьКонтакноеЛицоВГруппаШапкаПраво(Форма);
	ИначеЕсли ИмяФормы = "Документ.ОплатаОтПокупателя.Форма.ФормаДокумента"  
		Или ИмяФормы = "Документ.ОплатаПоставщику.Форма.ФормаДокумента" Тогда
		ВставитьКонтакноеЛицоНаФорму(Форма);
	КонецЕсли;
	// КонецДоработки
	
КонецПроцедуры

#КонецОбласти             

#Область СлужебныйПрограммныйИнтерфейс 

Процедура ДобавитьКонтакноеЛицоВГруппаШапкаПраво(Форма) Экспорт

	// НачалоДоработки   
	// Выводит поле "Контактное лицо" на форму документов 
	ПолеВвода = Форма.Элементы.Добавить("КонтактноеЛицо", Тип("ПолеФормы"), Форма.Элементы.ГруппаШапкаПраво);
	ПолеВвода.Вид = ВидПоляФормы.ПолеВвода;
	ПолеВвода.ПутьКДанным = "Объект.Д_КонтактноеЛицо";
	// КонецДоработки 
	
КонецПроцедуры         

Процедура ВставитьКонтакноеЛицоНаФорму(Форма) Экспорт

	// НачалоДоработки                                                                
	// Выводит поле "Контактное лицо" на форму документов      
	ПолеВвода = Форма.Элементы.Вставить("КонтактноеЛицо", Тип("ПолеФормы"), , Форма.Элементы.СуммаДокумента);
	ПолеВвода.Вид = ВидПоляФормы.ПолеВвода;
	ПолеВвода.ПутьКДанным = "Объект.Д_КонтактноеЛицо";
	// КонецДоработки      
	
КонецПроцедуры

Процедура ДобавитьСогласованнаяСкидкаИПересчитатьВГруппаШапкаЛево(Форма) Экспорт

	// НачалоДоработки 
	// В документ "Заказ покупателя" добавляет поле "Согласованная скидка" и команду "Пересчитать таблицу"
	Группа = Форма.Элементы.Добавить("Скидка", Тип("ГруппаФормы"), Форма.Элементы.ГруппаШапкаЛево);    
	Группа.Вид = ВидГруппыФормы.ОбычнаяГруппа;
    Группа.Отображение = ОтображениеОбычнойГруппы.Нет;
    Группа.ОтображатьЗаголовок = Ложь;
    Группа.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Горизонтальная;

	ПолеВвода = Форма.Элементы.Добавить("СогласованнаяСкидка", Тип("ПолеФормы"), Форма.Элементы.Скидка);
	ПолеВвода.Вид = ВидПоляФормы.ПолеВвода;
	ПолеВвода.ПутьКДанным = "Объект.Д_СогласованнаяСкидка";
	ПолеВвода.УстановитьДействие("ПриИзменении", "Д_СогласованнаяСкидкаПриИзменении"); 
	
	Команда = Форма.Команды.Добавить("ПересчитатьТаблицу");
	Команда.Заголовок = "Пересчитать таблицу";
	Команда.Действие = "Д_КомандаПересчитатьТаблицу";
	
	КнопкаФормы = Форма.Элементы.Добавить("КнопкаПересчитатьТаблицу", Тип("КнопкаФормы"), Форма.Элементы.Скидка);
	КнопкаФормы.ИмяКоманды = "ПересчитатьТаблицу";
	КнопкаФормы.Вид = ВидКнопкиФормы.ОбычнаяКнопка;
	// КонецДоработки 
	
КонецПроцедуры         

#КонецОбласти
